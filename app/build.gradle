apply plugin: 'com.android.application'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "ax.stardust.skvirrel"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "v1.0beta"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "$applicationId-$versionName")
    }

    signingConfigs {
        release
    }

    // push version name from gradle config into string resource "version_name"
    applicationVariants.all { variant ->
        variant.resValue "string", "version_name", variant.versionName
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField 'boolean', 'DEBUG', 'true'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    // configure keystore if git action is running the build
    if (System.getenv("CI") == "true") {
        def path = System.getProperty("user.home") + "/work/_temp/keystore/"
        def files = new File(path).listFiles()

        if (files != null) {
            def keystoreFile = files.first()
            keystoreFile.renameTo("keystore/skvirrel.jks")
        }

        storeFile = file("keystore/skvirrel.jks")
        storePassword System.getenv("SIGNING_STORE_PASSWORD")
        keyAlias System.getenv("SIGNING_KEY_ALIAS")
        keyPassword System.getenv("SIGNING_KEY_PASSWORD")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
    }

    lintOptions {
        abortOnError false // todo: change this
        ignoreWarnings false
    }

    buildToolsVersion '29.0.2'
}

dependencies {
    def appcompat_version = "1.3.1"
    def lombok_version = "1.18.22"
    def acra_version = "5.8.4"

    //noinspection AnnotationProcessorOnCompilePath
    compileOnly "org.projectlombok:lombok:$lombok_version"
    annotationProcessor "org.projectlombok:lombok:$lombok_version"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    // For loading and tinting drawables on older versions of the platform
    implementation "androidx.appcompat:appcompat-resources:$appcompat_version"
    implementation 'com.yahoofinance-api:YahooFinanceAPI:3.15.0'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "ch.acra:acra-mail:$acra_version"
    implementation "ch.acra:acra-dialog:$acra_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.8.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
